apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven-publish'

dependencies {
    implementation libraries.kotlinStd
    implementation libraries.rxJava
    implementation libraries.jCommander
    implementation libraries.commanderOs
    implementation libraries.commanderAndroid
    implementation libraries.apacheCommonsIo
    implementation libraries.apacheCommonsLang
    implementation libraries.gson
    implementation libraries.dexParser
}

dependencies {
    testImplementation libraries.spek
    testImplementation libraries.junitPlatformRunner
    testImplementation libraries.spekJunitPlatformEngine
    testImplementation libraries.assertJ
}

repositories {
    jcenter()
}

junitPlatform {
    platformVersion = versions.junitPlatform

    filters {
        engines {
            include 'spek'
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task validatePublishing {
    doLast {
        validateTagAndVersion()
    }
}

publish.dependsOn validatePublishing

def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'gojuno'
            name 'Juno Inc.'
            email 'opensource@gojuno.com'
        }
    }
}

publishing {
    publications {
        ComposerPublication(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            groupId 'com.gojuno.composer'
            artifactId 'composer'
            version projectVersion()

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Reactive Android Instrumentation Test Runner.')
                root.appendNode('name', 'Composer')
                root.appendNode('url', 'https://github.com/gojuno/composer')
                def repositories = root.appendNode("repositories")

                def repository = repositories.appendNode("repository")
                repository.appendNode("id", "bintray")
                repository.appendNode("name", "bintray")
                repository.appendNode("layout", "default")
                repository.appendNode("url", "https://jcenter.bintray.com")
                def snapshots = repository.appendNode("snapshots")
                snapshots.appendNode("enabled", false)

                def mavenCentral = repositories.appendNode("repository")
                mavenCentral.appendNode("id", "maven central")
                mavenCentral.appendNode("name", "maven central")
                mavenCentral.appendNode("layout", "default")
                mavenCentral.appendNode("url", "https://repo1.maven.org/maven2")
                def snapshotsMavenCentral = mavenCentral.appendNode("snapshots")
                snapshotsMavenCentral.appendNode("enabled", false)

                root.children().last() + pomConfig
            }
        }
    }

    repositories {
        maven {
            url = System.getenv('NEXUS_URL')
            allowInsecureProtocol = true
            credentials {
                username = System.getenv('NEXUS_USER')
                password = System.getenv('NEXUS_PWD')
            }
        }
    }
}